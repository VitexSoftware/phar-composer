#!groovy

// Current version of this Pipeline https://raw.githubusercontent.com/VitexSoftware/multiflexi-buildimages/refs/heads/main/Test/multiflexi-halloworld/Jenkinsfile

String[] distributions = ['debian:bookworm', 'debian:trixie', 'debian:forky', 'ubuntu:jammy', 'ubuntu:noble']

String vendor = 'vitexsoftware'
String imagePrefix = 'multiflexi-'
//String distroFamily = ''

properties([
    copyArtifactPermission('*')
])
node() {
    ansiColor('xterm') {
        stage('SCM Checkout') {
            checkout scm
        }
    }
}

def branches = [:]
distributions.each { distro ->
    branches[distro] = {
        def distroName = distro
        println  "Dist:" + distroName

        def dist = distroName.split(':')
        def distroCode = dist[1]
        def buildImage = ''
        def artifacts = []
        def buildVer = ''

        node {
            ansiColor('xterm') {
                stage('Checkout ' + distroName) {
                    checkout scm
                    def imageName = vendor + '/' + imagePrefix + distroCode + ':latest'
                    buildImage = docker.image(imageName)
                    sh 'git checkout debian/changelog'
                    def version = sh (
                        script: 'dpkg-parsechangelog --show-field Version',
                        returnStdout: true
                    ).trim()
                    buildVer = version + '.' + env.BUILD_NUMBER  + '~' + distroCode
                }
                stage('Build ' + distroName) {
                    buildImage.inside {
                        sh 'dch -b -v ' + buildVer  + ' "' + env.BUILD_TAG  + '"'
                        sh 'sudo apt-get update --allow-releaseinfo-change'
                        sh 'sudo chown jenkins:jenkins ..'
                        sh 'debuild-pbuilder  -i -us -uc -b'
                        sh 'mkdir -p $WORKSPACE/dist/debian/ ; rm -rf $WORKSPACE/dist/debian/* ; for deb in $(cat debian/files | awk \'{print $1}\'); do mv "../$deb" $WORKSPACE/dist/debian/; done'
                        artifacts = sh (
                            script: "cat debian/files | awk '{print \$1}'",
                            returnStdout: true
                        ).trim().split('\n')

                        // Stash artifacts to ensure they are available for archiving outside the container
                        stash name: "deb-${distroCode}", includes: "dist/debian/*"
                    }
                }

                stage('Test ' + distroName) {
                    buildImage.inside {
                        def debconf_debug = 0 //Set to "5" or "developer" to debug debconf
                        sh 'cd $WORKSPACE/dist/debian/ ; dpkg-scanpackages . /dev/null | gzip -9c > Packages.gz; cd $WORKSPACE'
                        sh 'echo "deb [trusted=yes] file://///$WORKSPACE/dist/debian/ ./" | sudo tee /etc/apt/sources.list.d/local.list'
                        sh 'sudo apt-get update --allow-releaseinfo-change'
                        sh 'echo "INSTALATION"'
                        artifacts.each { deb_file ->
                            if (deb_file.endsWith('.deb')) {
                                sh 'echo -e "${GREEN} installing ' + deb_file + ' on `lsb_release -sc` ${ENDCOLOR} "'
                                sh 'sudo DEBIAN_FRONTEND=noninteractive DEBCONF_DEBUG=' + debconf_debug + ' apt-get -y install $WORKSPACE/dist/debian/' + deb_file
                            }
                        }
                    }
                }
                stage('Archive artifacts ' + distroName ) {
                    // Ensure artifacts are present in this workspace (robust against earlier steps moving/removing them)
                    unstash "deb-${distroCode}"

                    // Print which artifacts we plan to archive
                    artifacts.findAll { it.endsWith('.deb') }.each { deb_file ->
                        println "Archiving artifact: " + deb_file
                    }

                    // Archive all .deb and .buildinfo for this distro
                    archiveArtifacts artifacts: "dist/debian/*~${distroCode}_*.deb", fingerprint: true
                    archiveArtifacts artifacts: "dist/debian/*~${distroCode}_*.buildinfo", allowEmptyArchive: true

                    // Cleanup: remove any produced files named in debian/files
                    sh '''
                        set -e
                        if [ -f debian/files ]; then
                          while read -r file _; do
                            [ -n "$file" ] || continue
                            rm -f "dist/debian/$file" || true
                            rm -f "../$file" || true
                            rm -f "$WORKSPACE/$file" || true
                          done < debian/files
                        fi
                    '''
                }
            }
        }
    }
}
parallel branches

if (!currentBuild.result || currentBuild.result == 'SUCCESS') {
    build job: 'MultiFlexi-publish',
      wait: false,
      parameters: [
        string(name: 'UPSTREAM_JOB', value: env.JOB_NAME),
        string(name: 'UPSTREAM_BUILD', value: env.BUILD_NUMBER),
        string(name: 'REMOTE_SSH', value: 'multirepo@repo.multiflexi.eu'),
        string(name: 'REMOTE_REPO_DIR', value: '/srv/repo'),
        string(name: 'COMPONENT', value: 'main'),
        string(name: 'DEB_DIST', value: '')
      ]
}
